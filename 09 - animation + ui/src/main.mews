yarn ball main

takes std.graphic
takes mewly
takes palette
takes controls
takes utils.canvas
takes utils.position
from std.graphic takes Rectangle
from animation.player takes AnimationPlayer
from utils.fps takes FpsCounter

------------------------------------------------------------------------
-- Animation:
------------------------------------------------------------------------
mew anim!!! = new AnimationPlayer(mewly.frames, mewly.frame_rate)

=^.x.^= draw_anim()
  mew pos = position.center(mewly.width, mewly.height)
  do anim.draw <- pos.x, pos.y
~meow

=^.x.^= set_anim_speed(percentage)
  mew new_frame_rate = std.floor(mewly.frame_rate * (percentage / 100))
  do anim.set_frame_rate <- new_frame_rate
~meow

------------------------------------------------------------------------
-- Title:
------------------------------------------------------------------------
mew title_string!!! = "set animation speed"
mew title_options!! = =^-x-^= [
  size: 12,
  color: palette.light_pink,
]

=^.x.^= draw_text()
  mew text_size = graphic.measure_text(title_string, title_options)

  mew x = (canvas.width - text_size.width) / 2
  mew y = position.row(1)
  do graphic.write <- title_string, x, y, title_options
~meow

------------------------------------------------------------------------
-- Controls:
------------------------------------------------------------------------
mew controls_x!!! = position.center(
    controls.get_width(),
    controls.get_height(),
  ).x
mew controls_y!!! = position.row(6)

controls.set_position(controls_x, controls_y)

=^.x.^= draw_controls()
  controls.draw()
  controls.update()
  do set_anim_speed <- controls.speed
~meow

------------------------------------------------------------------------
-- FPS:
------------------------------------------------------------------------
mew fps!!! = new FpsCounter()
mew fps_options!!! = =^-x-^= [ color: palette.cream, ]

=^.x.^= draw_fps()
  fps.tick()
  fps.draw(1, 0, fps_options)
~meow

------------------------------------------------------------------------
-- Game loop:
------------------------------------------------------------------------
=^.x.^= game_loop()
  do graphic.paint <- palette.dark_purple
  draw_anim()
  draw_text()
  draw_controls()
  draw_fps()
~meow

do graphic.thumbnail <- game_loop
do graphic.init <- game_loop
