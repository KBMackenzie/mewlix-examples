yarn ball ui.button

takes std.graphic

~( ^.x.^)> 
A clowder defining the behavior of a button.

Its constructor accepts:
  1. A Rectangle box defining its position + dimensions.
  2. A function to call when the button is clicked.

It contains the following methods:
  update(): Should be called every frame.
  is_hovered(): Returns true if the button is being hovered.
  is_clicked(): Returns true if the button has been clicked.
<(^.x.^ )~

clowder Button
  =^.x.^= wake(rect, callback)
    home.rect = rect
    home.callback = callback
  ~meow

  =^.x.^= is_hovered()
    bring home.rect.contains(graphic.mouse_position())
  ~meow

  =^.x.^= is_clicked()
    bring graphic.mouse_click() and home.is_hovered()
  ~meow

  =^.x.^= update()
    look if home.is_clicked()
      home.callback()
    ~meow
  ~meow

  =^.x.^= set_position(x, y)
    home.rect.x = x
    home.rect.y = y
  ~meow
~meow
