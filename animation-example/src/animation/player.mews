yarn ball animation.player

takes std.graphic

~( ^.x.^)> 
A clowder designed for easily playing looping frame animations.

Its constructor accepts:
  1. A shelf of sprite keys. The last frame should be at the *top* of the shelf.
  2. The frame rate for the animation.

It contains the following methods:
  draw(): Draw the animation and update the animation state.
  set_frame_rate(): Change the frame rate for the animation.
<(^.x.^ )~

clowder AnimationPlayer
  =^.x.^= wake(frames, frame_rate)
    assert not std.empty(frames)
    assert frame_rate > 0

    -- Reverse to ensure frames will play
    -- in the expected order. c:
    frames = std.reverse(frames)

    home.frames = frames
    home.frame_rate = frame_rate

    home.timer = 0.0
    home.frame_stack = knock over frames
    home.current_frame = peek frames
  meowmeow

  =^.x.^= _next_frame()
    peek if std.empty(home.frame_stack)
      home.frame_stack = home.frames
    meowmeow

    home.timer = 0.0
    home.current_frame = peek home.frame_stack
    home.frame_stack = knock over home.frame_stack
  meowmeow

  =^.x.^= draw(x, y)
    home.timer = home.timer + graphic.delta()

    peek if home.timer >= (1 / home.frame_rate)
      home._next_frame()
    meowmeow

    graphic.draw(home.current_frame, x, y)
  meowmeow

  =^.x.^= set_frame_rate(frame_rate)
    assert frame_rate > 0
    home.frame_rate = frame_rate
  meowmeow
meowmeow
