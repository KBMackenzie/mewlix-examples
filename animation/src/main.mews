yarn ball main

takes std.graphic
takes mewly
takes palette
takes utils.canvas
takes utils.position
takes speed_controls
from std.graphic takes Rectangle
from animation.player takes AnimationPlayer
from utils.fps takes FpsCounter

------------------------------------------------------------------------
-- Animation:
------------------------------------------------------------------------
mew anim = new AnimationPlayer(mewly.frames, mewly.frame_rate)

=^.x.^= draw_anim()
  mew pos = position.center(mewly.width, mewly.height)
  anim.draw(pos.x, pos.y)
meowmeow

=^.x.^= set_anim_speed(percentage)
  mew new_frame_rate = std.floor(mewly.frame_rate * (percentage / 100))
  anim.set_frame_rate(new_frame_rate)
meowmeow

------------------------------------------------------------------------
-- Title:
------------------------------------------------------------------------
mew title_string = "set animation speed"

mew title_options = =^-x-^= [
  size: 12,
  color: palette.light_pink,
]

=^.x.^= draw_text()
  mew text_size = graphic.measure_text(title_string, title_options)

  mew x = (canvas.width - text_size.width) / 2
  mew y = position.row(1)
  graphic.write(title_string, x, y, title_options)
meowmeow

------------------------------------------------------------------------
-- Controls:
------------------------------------------------------------------------
=^.x.^= draw_controls()
  speed_controls.update()
  speed_controls.draw()
  set_anim_speed(speed_controls.speed)
meowmeow

------------------------------------------------------------------------
-- FPS:
------------------------------------------------------------------------
mew fps = new FpsCounter()

mew fps_options = =^-x-^= [
  color: palette.cream,
]

=^.x.^= draw_fps()
  fps.tick()
  fps.draw(1, 0, fps_options)
meowmeow

------------------------------------------------------------------------
-- Game loop:
------------------------------------------------------------------------
=^.x.^= game_loop()
  graphic.paint(palette.dark_purple)
  draw_anim()
  draw_text()
  draw_controls()
  draw_fps()
meowmeow

game_loop |> graphic.thumbnail
game_loop |> graphic.init
