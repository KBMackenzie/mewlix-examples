yarn ball main

takes std.graphic
takes mewly_anim
takes utils.canvas
from utils.position takes center, row
from animation.player takes AnimationPlayer
from utils.fps takes FpsCounter

------------------------------------------------------------------------
-- Animation:
------------------------------------------------------------------------
mew anim = new AnimationPlayer(mewly_anim.frames, mewly_anim.frame_rate)

=^.x.^= draw_anim()
  mew pos = center(mewly_anim.width, mewly_anim.height)
  anim.draw(pos.x, pos.y)
meowmeow

------------------------------------------------------------------------
-- Text:
------------------------------------------------------------------------
mew text_options = =^-x-^= [
  size: 12,
  color: "#fff",
  align: "center",
  base: "middle",
]

=^.x.^= draw_text()
  mew x = canvas.width / 2
  mew y = row(1)
  graphic.write("animation example", x, y, text_options)
meowmeow

------------------------------------------------------------------------
-- FPS:
------------------------------------------------------------------------
mew fps = new FpsCounter()
mew fps_text_options = =^-x-^= [
  color: "#fff"
]

=^.x.^= draw_fps()
  fps.tick()
  fps.draw(1, 0, fps_text_options)
meowmeow

------------------------------------------------------------------------
-- Game loop:
------------------------------------------------------------------------
=^.x.^= game_loop()
  graphic.paint("#6e6e6e")
  draw_anim()
  draw_text()
  draw_fps()
meowmeow

graphic.thumbnail(game_loop)
graphic.init(game_loop)
