yarn ball main

takes std.graphic
takes mewly_anim
takes utils.canvas
from utils.position takes center, row
from animation.player takes AnimationPlayer
from utils.fps takes FpsCounter

------------------------------------------------------------------------
-- Animation:
------------------------------------------------------------------------
mew anim = new AnimationPlayer(mewly_anim.frames, mewly_anim.frame_rate)

=^.x.^= draw_anim()
  mew pos = center(mewly_anim.width, mewly_anim.height)
  anim.draw(pos.x, pos.y)
meowmeow

------------------------------------------------------------------------
-- Title:
------------------------------------------------------------------------
mew title_options = =^-x-^= [
  size: 12,
  color: "#fff",
]
mew title_string = "animation example"

=^.x.^= draw_text()
  mew metrics = graphic.measure_text(title_string, title_options)
  mew x = (canvas.width - metrics.width) / 2
  mew y = row(1) - (metrics.height / 2)
  graphic.write(title_string, x, y, title_options)
meowmeow

------------------------------------------------------------------------
-- FPS:
------------------------------------------------------------------------
mew fps = new FpsCounter()
mew fps_options = =^-x-^= [
  color: "#fff"
]

=^.x.^= draw_fps()
  fps.tick()
  fps.draw(1, 0, fps_options)
meowmeow

------------------------------------------------------------------------
-- Game loop:
------------------------------------------------------------------------
=^.x.^= game_loop()
  graphic.paint("#6e6e6e")
  draw_anim()
  draw_text()
  draw_fps()
meowmeow

graphic.thumbnail(game_loop)
graphic.init(game_loop)
