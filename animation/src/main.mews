yarn ball main

takes std.graphic
takes mewly
takes palette
takes utils.canvas
takes utils.position
takes speed_controls
from std.graphic takes Rectangle
from animation.player takes AnimationPlayer
from utils.fps takes FpsCounter

------------------------------------------------------------------------
-- Animation:
------------------------------------------------------------------------
mew anim = new AnimationPlayer(mewly.frames, mewly.frame_rate)

=^.x.^= draw_anim()
  mew pos = position.center(mewly.width, mewly.height)
  anim.draw(pos.x, pos.y)
meowmeow

------------------------------------------------------------------------
-- Title:
------------------------------------------------------------------------
mew title_string = "animation example"

mew title_options = =^-x-^= [
  size: 14,
  color: palette.light_pink,
]

=^.x.^= draw_text()
  mew text_size = graphic.measure_text(title_string, title_options)

  mew x = (canvas.width - text_size.width) / 2
  mew y = position.row(1) - text_size.height / 2
  graphic.write(title_string, x, y, title_options)
meowmeow

------------------------------------------------------------------------
-- Controls:
------------------------------------------------------------------------
=^.x.^= draw_controls()
  speed_controls.update()
  speed_controls.draw()
meowmeow

------------------------------------------------------------------------
-- FPS:
------------------------------------------------------------------------
mew fps = new FpsCounter()

mew fps_options = =^-x-^= [
  color: palette.cream,
]

=^.x.^= draw_fps()
  fps.tick()
  fps.draw(1, 0, fps_options)
meowmeow

------------------------------------------------------------------------
-- Game loop:
------------------------------------------------------------------------
=^.x.^= game_loop()
  graphic.paint(palette.dark_purple)
  draw_anim()
  draw_text()
  draw_controls()
  draw_fps()
meowmeow

graphic.thumbnail(game_loop)
graphic.init(game_loop)
