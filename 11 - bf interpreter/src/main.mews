yarn ball main

takes std.console
from brainfuck.parse takes parse
from brainfuck.eval takes evaluate

do console.name <- "brainfuck"
do console.accepted_files <- [".b", ".bf"]

cat tree State
  Parse
  Interpret
~meow

mew state = State.Parse
mew tokens

=^.x.^= opening_message(input)
  look if state == State.Parse
    bring "Enter a Brainfuck string (or upload a *.b file):"
  else just
    bring "Enter input for the program:"
  ~meow
~meow

=^.x.^= run_interpreter(input)
  look if state == State.Parse
    watch attentively
      tokens = parse(input)
      state = State.Interpret
    pounce on error
      look if error.id == std.error.ExternalError
        rethrow
      ~meow
      bring :3"Caught error: [error.message]"
    ~meow
  or if state == State.Interpret
    mew output! = evaluate(tokens, input)
    tokens = nothing
    state = State.Parse
    bring :3"\[Output:\]\n[output]\n\n"
  ~meow
~meow

do console.run <- run_interpreter, opening_message
