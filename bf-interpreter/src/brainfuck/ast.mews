yarn ball brainfuck.ast

from utils.constants takes constants

clowder Token
  =^.x.^= evaluate(_)
    throw "Not implemented!"
  meowmeow
meowmeow

clowder RepeatToken is Token
  =^.x.^= wake(count)
    assert count >= 1
    home.count = count
  meowmeow
meowmeow

clowder MoveLeft is RepeatToken
  =^.x.^= evaluate(state)
    state.move(-home.count)
  meowmeow
meowmeow

clowder MoveRight is RepeatToken
  =^.x.^= evaluate(state)
    state.move(home.count)
  meowmeow
meowmeow

clowder Increment is RepeatToken
  =^.x.^= evaluate(state)
    state.add(home.count)
  meowmeow
meowmeow

clowder Decrement is RepeatToken
  =^.x.^= evaluate(state)
    state.add(-home.count)
  meowmeow
meowmeow

clowder Puts is RepeatToken
  =^.x.^= evaluate(state)
    state.puts(home.count)
  meowmeow
meowmeow

clowder Gets is RepeatToken
  =^.x.^= evaluate(state)
    state.gets(home.count)
  meowmeow
meowmeow

clowder Loop is Token
  =^.x.^= wake(tokens)
    home.tokens = tokens
  meowmeow

  =^.x.^= run(state)
    it's raining home.tokens!! catch a token!!
      token.evaluate(state)
    meowmeow
  meowmeow

  =^.x.^= evaluate(state)
    stare while state.look() != 0
      home.run(state)
    meowmeow
  meowmeow
meowmeow
