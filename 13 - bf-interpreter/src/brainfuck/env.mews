yarn ball brainfuck.env

from std.curry takes map

clowder Environment
  =^.x.^= wake()
    home.cells    = std.table()
    home.pointer  = 0

    -- We will store the output in this clowder and print it once a script has finished running,
    -- Though this is ill-advised, it performs better than DOM manipulation on every '.'.
    home.output   = ""
  ~meow

  =^.x.^= look()
    bring home.cells.get(home.pointer) or 0
  ~meow

  =^.x.^= move(offset)
    home.pointer = home.pointer + offset
  ~meow

  =^.x.^= add(value)
    home.cells.add(home.pointer, (home.look() + value) % 256)
  ~meow

  =^.x.^= set(value)
    home.cells.add(home.pointer, value % 256)
  ~meow

  =^.x.^= puts()
    home.output = home.output .. std.char(home.look())
  ~meow

  =^.x.^= gets()
    look if home.input == nothing
      home.input = =^-x-^= [
        string: listen "Enter input to be read by the program:",
        index: 0,
      ]
    ~meow

    mew char = std.poke(home.input.string, home.input.index)

    look if char != nothing
      home.input.index = home.input.index + 1
      home.set(std.bap(char))
    ~meow
  ~meow

  =^.x.^= flush_input()
    home.input = nothing
  ~meow

  =^.x.^= get_output()
    bring home.output
  ~meow
~meow
