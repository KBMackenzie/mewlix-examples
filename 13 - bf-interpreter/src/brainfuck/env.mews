yarn ball brainfuck.env

from std.curry takes map

clowder Environment
  =^.x.^= wake()
    home.cells    = std.table()
    home.pointer  = 0

    -- We will store the output in this clowder and print it once a script has finished running,
    -- Though this is ill-advised, it performs better than DOM manipulation on every '.'.
    home.output   = ""
  meowmeow

  =^.x.^= look()
    bring home.cells.get(home.pointer) or 0
  meowmeow

  =^.x.^= move(offset)
    home.pointer = home.pointer + offset
  meowmeow

  =^.x.^= add(value)
    home.cells.add(home.pointer, (home.look() + value) % 256)
  meowmeow

  =^.x.^= set(value)
    home.cells.add(home.pointer, value % 256)
  meowmeow

  =^.x.^= puts()
    home.output = home.output .. std.char(home.look())
  meowmeow

  =^.x.^= gets()
    peek if home.input == nothing
      home.input = =^-x-^= [
        string: listen "Enter input to be read by the program:",
        index: 0,
      ]
    meowmeow

    mew char = std.poke(home.input.string, home.input.index)

    peek if char != nothing
      home.input.index = home.input.index + 1
      home.set(std.bap(char))
    meowmeow
  meowmeow

  =^.x.^= flush_input()
    home.input = nothing
  meowmeow

  =^.x.^= get_output()
    bring home.output
  meowmeow
meowmeow
