yarn ball brainfuck.env

clowder Input
  =^.x.^= wake(input)
    home.input = input
    home.index = 0
  ~meow

  =^.x.^= get()
    mew head!! = std.poke(home.input, home.index)
    home.index = home.index + 1
    bring head
  ~meow
~meow

clowder Environment
  =^.x.^= wake(stdin)
    home.cells    = std.table()
    home.pointer  = 0
    home.output   = ""
    home.input = new Input(stdin)
  ~meow

  =^.x.^= peek()
    bring home.cells.get(home.pointer) or 0
  ~meow

  =^.x.^= move(offset)
    home.pointer = home.pointer + offset
  ~meow

  =^.x.^= add(value)
    home.cells.add(home.pointer, (home.peek() + value) % 256)
  ~meow

  =^.x.^= set(value)
    home.cells.add(home.pointer, value % 256)
  ~meow

  =^.x.^= puts(count)
    stare while count > 0
      home.output = home.output .. std.char(home.peek())
      count = count - 1
    ~meow
  ~meow

  =^.x.^= gets()
    mew char! = home.input.get()
    look if char != nothing
      home.set(std.bap(char))
    ~meow
  ~meow

  =^.x.^= flush_input()
    home.input = nothing
  ~meow

  =^.x.^= get_output()
    bring home.output
  ~meow
~meow
