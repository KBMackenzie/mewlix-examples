yarn ball brainfuck.env

clowder Environment
  =^.x.^= wake()
    home.cells = std.table()
    home.pointer = 0
  meowmeow

  =^.x.^= look()
    bring home.cells.get(home.pointer) or 0
  meowmeow

  =^.x.^= move(incr)
    home.pointer = home.pointer + incr
  meowmeow

  =^.x.^= set(value)
    home.cells.add(home.pointer, value % 256)
  meowmeow

  =^.x.^= modify(value)
    home.cells.add(home.pointer, (home.look() + value) % 256)
  meowmeow

  =^.x.^= pop_input()
    peek if home.input == nothing
      home.input = =^-x-^= [
        string: listen "Enter program input:",
        index: 0,
      ]
    meowmeow

    home.input.index = home.input.index + 1
    bring std.poke(home.input.string, home.input.index) or 0
  meowmeow
meowmeow
