yarn ball brainfuck.env

from std.curry takes map

clowder Environment
  =^.x.^= wake()
    home.cells = std.table()
    home.pointer = 0
    home.output = []
  meowmeow

  =^.x.^= look()
    bring home.cells.get(home.pointer) or 0
  meowmeow

  =^.x.^= move(incr)
    home.pointer = home.pointer + incr
  meowmeow

  =^.x.^= set(value)
    home.cells.add(home.pointer, value % 256)
  meowmeow

  =^.x.^= modify(value)
    home.cells.add(home.pointer, (home.look() + value) % 256)
  meowmeow

  =^.x.^= puts()
    home.output = home.output .. std.char(home.look())
  meowmeow

  =^.x.^= gets()
    peek if home.input == nothing
      home.input = =^-x-^= [
        string: listen "Enter input to be read by the program:",
        index: 0,
      ]
    meowmeow

    mew char = std.poke(home.input.string, home.input.index)

    peek if char != nothing
      home.input.index = home.input.index + 1
      char |> std.bap |> home.set
    meowmeow
  meowmeow

  =^.x.^= flush_input()
    home.input = nothing
  meowmeow

  =^.x.^= get_output()
    bring home.output
  meowmeow
meowmeow
